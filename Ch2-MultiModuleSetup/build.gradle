buildscript {
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${property('springboot.version')}"
    }
    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/plugins-release' }
    }
}

allprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'idea'

    dependencyManagement {
        imports {
            mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
        }

        dependencies {
            dependency "org.springframework.geode:spring-gemfire-starter:${ext['spring-gemfire-starter.version']}"

            /***
             * SPECIFIC GEMFIRE MANAGEMENT, WE MUST OVERRIDE THESE
             */
            dependency "io.pivotal.gemfire:geode-core:${ext['gemfire.version']}"
            dependency "io.pivotal.gemfire:geode-wan:${ext['gemfire.version']}"
            dependency "io.pivotal.gemfire:geode-lucene:${ext['gemfire.version']}"
            dependency "io.pivotal.gemfire:geode-cq:${ext['gemfire.version']}"

            /**
             * SWAGGER
             */
            dependency "io.springfox:springfox-swagger2:${ext['swagger.version']}"
            dependency "io.springfox:springfox-swagger-ui:${ext['swagger.version']}"
        }
    }
}

subprojects {
    apply plugin: 'java'

    group = 'io.cloudtraining.sbdg'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '1.8'

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/libs-release" }
    }
    
    configurations.all {
        /***
         * for GemFire version 9.7.0 or lower,
         * exclude spring-boot-starter-logging to avoid log4j related exceptions
         */
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}